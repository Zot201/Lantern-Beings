buildscript {
  repositories {
    mavenCentral()
    maven {
      name = "forge"
      url = "http://files.minecraftforge.net/maven"
    }
    maven {
      name = "sonatype"
      url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
  }
  dependencies {
    classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
  }
}

apply plugin: 'fml'

version = "1.0.0.2-1.8"
group = "zotmc.lanternbeings"
archivesBaseName = "LanternBeings"

minecraft {
  version = "1.8-8.0.5.1012-1.8"
  runDir = "eclipse/assets"
  
  mappings = "snapshot_nodoc_20140930"
}

processResources {
  inputs.property "version", project.version
  inputs.property "mcversion", project.minecraft.version
  
  from(sourceSets.main.resources.srcDirs) {
    include 'mcmod.info'
    expand 'version':project.version, 'mcversion':project.minecraft.version
  }
  
  from(sourceSets.main.resources.srcDirs) {
    exclude 'mcmod.info'
  }
}


jar {
  manifest.attributes(
    'FMLCorePlugin' : 'zotmc.lanternbeings.loading.LbLoading',
    'FMLCorePluginContainsFMLMod' : 'true'
  )
}

[compileJava, compileTestJava].each {
  it.options.encoding = 'UTF-8'
  it.options.compilerArgs += ['-source', '1.7', '-target', '1.7']
}
reobf {
  deobfFile = new net.minecraftforge.gradle.delayed.DelayedFile(project, "build/tmp/deobfuscateJar/deobfed.jar")
}
